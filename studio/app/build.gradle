apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.vanniktech.android.junit.jacoco'

junitJacoco {
    jacocoVersion = jacocoVersion
}

android {
    def buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: 1
    def commitHash = System.getenv("GIT_COMMIT")

    compileSdkVersion 25
    buildToolsVersion "$rootProject.buildToolsVersion"

    if (System.getenv().containsKey("storeFile")) {
        apply from: 'signing.gradle'
    }

    defaultConfig {
        applicationId "nl.renedegroot.android.adbawake"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "int", "BUILD_NUMBER", buildNumber as String
        buildConfigField "String", "GIT_COMMIT", "\"" + commitHash + "\"" as String ?: "\"Unknown\""
        archivesBaseName = "adb-awake-$versionName"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dataBinding {
        enabled = true
    }

    kapt {
        generateStubs = true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$rootProject.constraintsLibraryVersion"
    compile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    kapt "com.android.databinding:compiler:$rootProject.dataBindingVersion"

    testCompile "junit:junit:$rootProject.junitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
}

repositories {
    mavenCentral()
}
